plugins {
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	id 'com.palantir.git-version' version '2.0.0'
	id 'java'
  id "net.ltgt.errorprone" version "3.1.0"
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.ow2.asm:asm:9.4'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
  errorprone("com.google.errorprone:error_prone_core:2.23.0")
}

tasks.withType(JavaCompile).configureEach {
	options.release = 8
	options.deprecation = true
  options.errorprone.disableWarningsInGeneratedCode = true
  options.errorprone.disableAllChecks = true
  options.errorprone.enable("NullOptional", "NullableOptional",
    "OptionalMapToOptional", "OptionalNotPresent", "OptionalEquality",
    "OptionalMapUnusedValue", "OptionalOfRedundantMethod", "UnnecessaryOptionalGet",
  )
}

def buildNumber = System.getenv('AI_BUILD_NUMBER')
def gitInfo = versionDetails()
version = System.getenv('AI_VERSION_NUMBER') ?: 'snapshot';
if (!gitInfo.isCleanTag) version += '.dirty'

jar {
	manifest {
		attributes (
			'Implementation-Title': project.name,
			'Implementation-Version': project.version,
			'Implementation-Vendor': 'yushijinhun',
			'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			'Automatic-Module-Name': 'moe.yushi.authlibinjector',
			'Premain-Class': 'moe.yushi.authlibinjector.Premain',
			'Agent-Class': 'moe.yushi.authlibinjector.Premain',
			'Can-Retransform-Classes': true,
			'Can-Redefine-Classes': true,
			'Git-Commit': gitInfo.gitHashFull,
			'Git-IsClean': gitInfo.isCleanTag,
			'Build-Number': buildNumber?:'-1'
		)
	}
}

processResources {
	from('LICENSE') {
		rename('LICENSE', 'authlib-injector.txt')
		into 'META-INF/licenses'
	}
}

test {
	useJUnitPlatform()
}

shadowJar {
	archiveClassifier.set(null)

	exclude 'META-INF/maven/**'
	exclude 'module-info.class'

	relocate 'org.objectweb.asm', 'moe.yushi.authlibinjector.internal.org.objectweb.asm'
}

defaultTasks 'clean', 'shadowJar'
